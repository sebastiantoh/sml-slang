grammar Sml;

/** Tokens */

WHITESPACE: [ \r\n\t]+ -> skip;
COMMENT: '(*' .*? '*)' -> skip;

INT
    : '~'? NUM
    ;

// Not supporting scientific notation for now since the tilde complicates thing
REAL
    : '~'? NUM '.' NUM
    ;

CHAR
    : '#"' ASCII '"'
    ;

STRING
    : '"' ASCII* '"'
    ;

fragment DIGIT: [0-9];

fragment NUM: DIGIT+;

fragment LETTER: [a-zA-Z];

fragment ESC: '\\' (["\\/bfnrt]);

fragment SAFECODEPOINT: ~ ["\\\u0000-\u001F];

// reference: https://stackoverflow.com/questions/66902057/antlr-how-to-accept-double-quotes-inside-double-quoted-string-while-parsing
fragment ASCII: ESC | SAFECODEPOINT;

TRUE: 'true';
FALSE: 'false';

LPAREN: '(';
RPAREN: ')';
LSQUARE: '[';
RSQUARE: ']';
REC: 'rec';
AND: 'and';
SEMICOLON: ';';
COMMA: ',';
UNIT: '()';
UNDERSCORE: '_';

SLASH: '/';
DIV: 'div';
MOD: 'mod';
STAR: '*';
PLUS: '+';
MINUS: '-';
CARET: '^';
CONS: '::';
AT: '@';
EQ: '=';
NEQ: '<>';
LT: '<';
GT: '>';
LTE: '<=';
GTE: '>=';
ANDALSO: 'andalso';
ORELSE: 'orelse';
QUOTE: '\'';

ID
    : LETTER (LETTER | DIGIT | '\'' | '_' )*
    | ( '!' | '%' | '&' | '$' | '#' | '+' | '-' | '/' | ':' |
        '<' | '=' | '>' | '?' | '@' | '\\' | '~' | '`' | '^' | '|' | '*' )+
    ;

VAR
    : QUOTE (LETTER | DIGIT | QUOTE | UNDERSCORE)*
    | QUOTE QUOTE (LETTER | DIGIT | QUOTE | UNDERSCORE)*
    ;

/** Productions */

con
    : INT              # Integer
    | REAL             # Real
    | CHAR             # Character
    | STRING           # String
    | (TRUE | FALSE)   # Boolean
    | UNIT             # Unit
    ;

exp
    : con                                                             # Constant
    | id=ID                                                           # ExpVariable
    | fn=exp arg=exp                                                  # Application
    // Precedence levels can be found on Page 98 of https://smlfamily.github.io/sml90-defn.pdf
    | op1=exp id=(SLASH | DIV | MOD | STAR) op2=exp                   # InfixApplication
    | op1=exp id=(PLUS | MINUS | CARET) op2=exp                       # InfixApplication
    | <assoc=right> op1=exp id=(CONS | AT) op2=exp                    # InfixApplication
    | op1=exp id=(EQ | NEQ | LT | GT | LTE | GTE) op2=exp             # InfixApplication
    | op1=exp id=ID op2=exp                                           # InfixApplication
    | LPAREN exp RPAREN                                               # Parentheses
    | LSQUARE (exp (COMMA exp)*)? RSQUARE                             # List
    | LPAREN exp (SEMICOLON exp)+ RPAREN                              # ExpSequence
    | 'let' decSequence 'in' exp (SEMICOLON exp)* 'end'               # LetExpression
    // TODO: add type annotation
    | op1=exp ANDALSO op2=exp                                         # Conjunction
    | op1=exp ORELSE op2=exp                                          # Disjunction
    | 'if' pred=exp 'then' cons=exp 'else' alt=exp                    # Conditional
    | 'case' exp 'of' matches                                         # CaseAnalysis
    | 'fn' matches                                                    # Function
    ;

// Note: this is named patmatch instead of match because
// Antlr's parser has a match function internally, which
// conflicts with the function generated by this rule
patmatch: pat '=>' exp;

matches: patmatch ('|' patmatch)*;

pat
    : con                                                             # PatConstant
    | UNDERSCORE                                                      # PatWildcard
    | UNIT                                                            # PatUnit
    | id=ID                                                           # PatVariable
    | pat1=pat CONS pat2=pat                                          # PatInfixConstruction
    | LPAREN pat RPAREN                                               # PatParentheses
    // TODO: add list
    // TODO: add type annotation
    ;

typ
    : VAR                                                             # TypeVariable
    // Note: based on the grammar spec it should be typ instead
    // of VAR. But I don't know when the other cases of typ is used
    // Also, it should be longid instead of id (but we don't plan
    // on supporting modules)
    // Example of this rule:
    // datatype ('a, 'b) pair = empty | cons of 'a * 'b
    | (VAR? | (VAR (COMMA VAR)+)) id=ID                               # TypeConstructor
    | LPAREN typ RPAREN                                               # TypeParentheses
    | argTy=typ '->' retTy=typ                                        # TypeFunction
    ;

dec
    : 'val' valbind (AND valbind)*                                    # ValueDecl
    | 'fun' funbind (AND funbind)*                                    # FunDecl
    | 'local' localDecs=decSequence 'in' decs=decSequence 'end'       # LocalDecl
    ;

// TODO: everywhere that uses "dec" in the
// grammar rules online should be replaced with this instead
decSequence: (dec SEMICOLON?)* dec;

valbind: REC? pat EQ exp;

funbind: funmatches;

funmatches: funmatch ('|' funmatch)*;

funmatch: id=ID pat pat* EQ exp;

prog: decSequence;
